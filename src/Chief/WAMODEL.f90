SUBROUTINE WAMODEL

! ---------------------------------------------------------------------------- !
!
!**** *WAMODEL* - 3-G WAM MODEL - TIME INTEGRATION OF WAVE FIELDS.
!
!     S.D. HASSELMANN  MPI       1.12.85
!
!     G. KOMEN         KNMI         6.86  MODIFIED FOR SHALLOW WATER
!     P. JANSSEN                          ASPECTS.
!
!     S.D. HASSELMANN  MPI       15.2.87  MODIFIED FOR CYBER 205.
!
!     P. LIONELLO      ISDGM      6.3.87  MODIFIED TO OUTPUT SWELL.
!
!     S.D. HASSELMANN  MPI        1.6.87  ALL VERSIONS COMBINED INTO
!                                         ONE MODEL. DEEP AND SHALLOW
!                                         WATER , CRAY AND CYBER 205
!                                         VERSION.
!
!     CYCLE_2 MODICIFATIONS:
!     ----------------------
!
!     L. ZAMBRESKY     GKSS        10.87  OPTIMIZED FOR CRAY, CYBER 205
!     H. GUNTHER
!
!     A. SPEIDEL       MPI          4.88  VARIABLE DIMENSIONS, INTERNAL
!                                         CHECKS (CFL-CRITERION).
!
!     A. SPEIDEL       MPI         11.88  CHANGES FOR CRAY-2.
!
!     K. HUBBERT       POL          6.89  DEPTH AND CURRENT REFRACTION.
!                                         PRECALCULATION OF TERMS IN
!                                         *PROPDOT*.
!                                         SOLVE WAVE ACTION EQUATION
!                                         FOR CURRENT REFRACTION.
!
!     CYCLE_3 MODICIFATIONS:
!     ----------------------
!
!     R. PORTZ , S.D. HASSELMANN   MPI          1990
!
!      - RESTRUCTURE MODEL TO CALL THE ACTUAL INTEGRATION IN TIME
!        AS A SUBROUTINE: WAMODEL. A SHELL PROGRAM "WAMSHELL" READS
!        OUTPUT FROM PREPROC AND COMPUTES THE WIND ARRAYS FOR THE
!        INTEGRATION PERIOD FROM PREWIND, WHICH HAS BEEN INCORPORATED
!        AS A SUBROUTINE.
!      - ALL INTERMEDIATE AND RESTART I/O IS DONE IN THE SUBROUTINE
!        WAMODEL AND INPREST.
!      - THE COMMON BLOCK IN THE PREPROCESSOR AND MODEL ARE MADE
!        COMPATIBLE.
!      - THE COMPUTATION OF SEVERAL PARAMETERS HAS BEEN TRANSFERRED
!        FROM THE MODEL TO PREPROC.
!      - DEPTH AND CURRENT REFRACTION HAS BEEN INCORPORATED INTO THE
!        MODEL.
!      - OPEN BOUNDARIES ARE INCORPORATED IN THE MODEL.
!      - SEVERAL MINOR ERRORS HAVE BEEN REMOVED.
!      - THE BUFFERED I/O FOR THE CYBER 205 HAS BEEN CHANGED INTO A
!        BINARY READ AND WRITE.
!
!     CYCLE_4 MODICIFATIONS:
!     ----------------------
!
!     L. ZAMBRESKY   GKSS/ECMWF   6.89  ECMWF SUB VERSION
!                                       BASED ON CYCLE_2.
!
!     H. GUNTHER     GKSS/ECMWF 10.89  ECMWF SUB VERSION REORGANIZED.
!                                      - COMMON BLOCK STRUCTURE.
!                                      - BLOCKING STRUCTURE.
!                                      - TIME COUNTING.
!                                      - GRIDDED OUTPUT FIELDS.
!                                      - HEADERS ADDED TO OUTPUT FILES.
!                                      - ERRORS IN PROPAGATION CORRECTED
!
!     P.A.E.M. JANSSEN KNMI      1990  COUPLED MODEL.
!
!     H. GUNTHER     GKSS/ECMWF  8.91  LOGARITHMIC DEPTH TABLES.
!                                      MPI CYCLE_3 AND ECMWF VERSIONS
!                                      COMBINED INTO CYCLE_4.
!
!     H. GUNTHER       GKSS    DECEMBER 2001
!                                    - DKRZ AND ECMWF VERSIONS INTEGRATED.
!                                    - FT 90 VERSION.
!                                    - DYNAMICAL ARRAYS.
!                                    - ICE FIELDS INCORPORATED
!                                    - INTEGRATED PARAMETER EXTENTED BY
!                                         TM1, TM2 PERIODS
!                                         MEAN DIRECTIONAL SPREAD
!                                    - OUTPUT OF INTEGRATED PARAMETER
!                                      FOR TOTAL, SEA AND SWELL IN ONE FILE.
!                                    - OUTPUT OF TOTAL, SEA AND SWELL SPECTRA 
!                                      IN ONE FILE. 
!                                    - SCRATCH FILES FOR WIND STORAGE REMOVED.
!                                    - DATE TIME GROUPS EXTENTED BY CENTURAY 
!                                      AND SECONDS. 
!                                    - PROPAAGATION TIMESTEP CAN BE LESS THAN
!                                      SOURCE FUNCTION TIME STEP.
!                                    - TIME INTERPOLATION OF BOUNDARY SPECTRA.
!                                    - SPECIFICATION OF SPECTRA OUTPUT SITES
!                                      MOVED FROM PREPROC TO CHIEF.
!
!*    PURPOSE.
!     --------
!
!       COMPUTATION OF THE 2-D FREQUENCY-DIRECTION WAVE SPECTRUM AT ALL
!       GRID POINTS FOR A GIVEN INITIAL SPECTRUM AND FORCING SURFACE
!       STRESS FIELD.
!
!     METHOD.
!     -------
!
!       GRID POINTS ARE LAT - LONG,VECTORIZATION IS ACHIEVED BY RUNNING
!       THROUGH THE GRID POINTS IN AN INNER LOOP ORGANIZED AS 1-D ARRAY
!       IN BLOCKS,-ALL COMPUTATIONS ARE CARRIED OUT FOR ONE BLOCK AT A
!       TIME (SEE "BLOCK STRUCTURE" BELOW)
!
!       ALL COMPONENTS OF THE SPECTRUM ARE COMPUTED PROGNOSTICALLY FROM
!       THE SPECTRAL TRANSPORT EQUATION UP TO A VARIABLE CUT-OFF
!       FREQUENCY = MAX(4*FPM,2.5*FMEAN),WHERE FPM IS THE
!       PIERSON MOSKOVITZ FREQUENCY AND FMEAN IS THE MEAN FREQUENCY,
!       BEYOND THE PROGNOSTIC CUTOFF A DIAGNOSTIC F**-5 TAIL IS ATTACHED
!       CONTINUOUSLY FOR EACH DIRECTION,
!
!       SOURCE FUNCTIONS ARE TAKEN FROM KOMEN ET AL(1984)
!
!       THE NONLINEAR TRANSFER IS PARAMETERIZED BY THE DISCRETE INTER-
!       ACTION APPROXIMATION OF HASSELMANN ET AL (1985B)
!
!       THE SOURCE FUNCTION AND THE ADVECTION TERM ARE INTEGRATED ON TWO
!       DIFFERENT TIME STEP LEVELS AND WITH DIFFERENT METHODS,-THE
!       ADVECTION TIME STEP IS A MULTIPLE OF THE SOURCE FUNCTION
!       TIME STEP.
!
!       THE SOURCE FUNCTIONS ARE INTEGRATED IMPLICITLY ACCORDING TO
!       HASSELMANN AND HASSELMANN (1985A),-THE RELEVANT FUNCTIONAL
!       DERIVATIVES OF THE INDIVIDUAL SOURCE FUNCTIONS REQUIRED FOR THE
!       SOLUTION OF THE IMPLICIT EQUATION ARE COMPUTED WITHIN THE SOURCE
!       FUNCTION SUBS,- THE TIME STEP IS TYPICALLY 20 MIN,
!
!       THE ADVECTION IS INTEGRATED BY A FIRST ORDER UPWIND SCHEME,ALSO
!       ACCORDING TO HASSELMANN AND HASSELMANN (1985A),-THE ADVECTIVE
!       TIMESTEP IS DEPENDENT ON THE FREQUENCY AND SPATIAL GRID IN
!       ACCORDANCE WITH CFL,
!
!       WINDS ARE TAKEN EVERY WIND TIME STEP.IF THE WIND TIME STEP IS
!       GREATER THAN THE SOURCE TERM TIME STEP DELTWIND/DELTSOURCE STEPS
!       ARE INTEGRATED WITH CONSTANT WINDS,
!       WIND TIME STEP,PROPAGATION TIME STEP AND SOURCE TERM TIME STEP
!       SHOULD HAVE INTEGER RATIOS, THEY ARE GIVEN IN SECONDS AT
!       FULL MINUTES.
!
!       ZERO ENERGY INFLUX IS ASSUMED AT COAST LINES. OPEN BOUNDARIES
!       ARE INCORPORATED IN THE MODEL, IF IT RUNS AS A NESTED GRID.
!
!       BLOCK STRUCTURE (SEE PREPROC FOR DETAILS):
!       SEA POINTS ARE COLLECTED INTO A 1-DIMENSIONAL ARRAY.
!       BLOCKS OF MAXIMALLY NSEA ELEMENTS.
!       SEA POINTS ARE COUNTED ALONG LINES OF LATITUDES FROM LEFT COAST
!       TO RIGHT COAST WORKING FROM SOUTH TO NORTH.
!       BLOCKS OVERLAP OVER TWO LATITUDE LINES,TO COMPUTE NORTH-SOUTH
!       ADVECTION TERMS.
!
!     REFERENCE.
!     ----------
!
!       SNYDER, R.L., F.W. DOBSON, J.A. ELLIOT, AND R.B. LONG:
!          ARRAY MEASUREMENTS OF ATMOSPHERIC PRESSURE FLUCTUATIONS
!          ABOVE SURFACE GRAVITY WAVES. J.FLUID MECH. 102, 1-59 ,1981.
!       G. KOMEN, S. HASSELMANN, K. HASSELMANN:
!          ON THE EXISTENCE OF A FULLY DEVELOPED WIND SEA SPECTRUM.
!          JPO,1984.
!       S. HASSELMANN, K. HASSELMANN, J.H. ALLENDER, T.P. BARNETT:
!          IMPROVED METHODS OF COMPUTING AND PARAMETERIZING THE
!          NONLINEAR ENERGY TRANSFER IN A GRAVITY WAVE SPECTRUM.
!          JPO, 1985.
!       S. HASSELMANN, K. HASSELMANN: A GLOBAL WAVE MODEL,
!          WAM REPORT,JUNE,30/1985.
!       P. JANSSEN, G. KOMEN: A SHALLOW WATER EXTENSION OF THE
!          3-G WAM-MODEL. WAM REPORT 1985.
!       THE WAMDI GROUP: THE WAM MODEL - A THIRD GENERATION OCEAN
!          WAVE PREDICTION MODEL. JPO, VOL. 18, NO. 12, 1988.
!       P.A.E.M JANSSEN: JPO, 1989 AND 1991.
!       K. HASSELMANN: TRANSPORT EQUATION OF FINITE DEPTH SURFACE
!          WAVE SPECTRUM IN TIME DPENDANT CURRENT AND DEPTH FIELD USING
!          NONCANONICAL SPACIAL (SPHERICAL) AND WAVE NUMBER (FRQUENCY-
!          DIRECTION) COORDINATES. WAM REPROT 1988.
!
! ---------------------------------------------------------------------------- !
!
!*     EXTERNALS.
!     -----------

USE WAM_SOURCE_MODULE,    ONLY:  &
&       IMPLSCH                     !! INTEGRATION OF SOURCE FUNCTIONS IN TIME.

USE WAM_GENERAL_MODULE,   ONLY:  &
&       INCDATE                     !! UPDATE DATE TIME GROUP.

USE WAM_PROPAGATION_MODULE, ONLY: &
&       PROPAGS                     !! PROPAGATION SCHEME.

USE WAM_BOUNDARY_MODULE, ONLY:   &
&       BOUNDARY_INPUT,          &  !! INPUT OF BOUNDARY VALUES.
&       BOUNDARY_OUTPUT             !! OUTPUT OF BOUNDARY VALUES.

USE WAM_OUTPUT_MODULE,    ONLY:  &
&       MODEL_OUTPUT_CONTROL        !! CONTROLS MODEL OUTPUT.

USE WAM_OUTPUT_SET_UP_MODULE, ONLY:  &
&       UPDATE_OUTPUT_TIME          !! UPDATES OUTPUT TIMES.

USE WAM_WIND_MODULE,      ONLY:  &
&       GET_WIND                    !! GETS A NEW WIND FIELD.

USE WAM_RESTART_MODULE,   ONLY:  &
&       SAVE_RESTART                !! SAVES RESTART FILES.

USE WAM_ICE_MODULE,       ONLY:  &
&       PUT_ICE                     !! PUTS ICE INDICATOR INTO DATA FILED.

! ---------------------------------------------------------------------------- !
!
!*    MODULE VARIABLES.
!     -----------------

USE WAM_NEST_MODULE, ONLY: COARSE, FINE

USE WAM_GRID_MODULE,     ONLY: DEPTH, INDEP

USE WAM_MODEL_MODULE,    ONLY: FL3, U10, UDIR, USTAR, TAUW, Z0   

USE WAM_TIMOPT_MODULE,   ONLY: CDATEE, CDTPRO, CDTSOU, IDELPRO, IDELT, CDATEWO

USE WAM_FILE_MODULE,     ONLY: IU06, ITEST, IDELRES, IREST, CDTRES,            &
&                              IU17, FILE17, IU20, FILE20,IU25, FILE25

USE WAM_ICE_MODULE,      ONLY: ICE_RUN

USE WAM_OUTPUT_SET_UP_MODULE, ONLY: CDTINTT, CDTSPT

USE WAM_PROPAGATION_MODULE, ONLY: NADV

! ---------------------------------------------------------------------------- !
!
!*    INTERFACE VARIABLES.
!     --------------------

IMPLICIT NONE

! ---------------------------------------------------------------------------- !
!
!*    LOCAL VARIABLES.
!     ----------------

INTEGER          :: KADV
CHARACTER*14     :: CDTSOE

! ---------------------------------------------------------------------------- !
!
!*    1. ADVECTION TIME LOOP.
!        --------------------

PROP: DO KADV = 1,NADV

   IF (ITEST.GE.2) THEN
      WRITE(IU06,*) '   SUB. WAMODEL: START OF PROPAGATION '
      WRITE(IU06,*) '      START DATE IS CDTPRO = ',CDTPRO
   END IF

!*    1.1 UPDATE TIMES.
!         -------------

   CALL INCDATE(CDTPRO,IDELPRO)     !! UPDATE END DATE OF PROPAGATION.

   CALL UPDATE_OUTPUT_TIME          !! UPDATE OUTPUT TIMES.

   IF (CDTRES.LT.CDTPRO) CALL INCDATE(CDTRES,IDELRES)

!*    1.2 COMPUTE OF PROPAGATION.
!         -----------------------

    CALL PROPAGS (FL3)

!*    1.3 SOURCE INTEGRATION AND LOOP.
!         -----------------------------

   CDTSOE = CDTSOU                  !! END DATE OF SOURCE INTEGRATION.
   CALL INCDATE (CDTSOE,IDELT)

   PHYSICS: DO WHILE (CDTSOE.LE.CDTPRO)
      IF (ITEST.GE.2) THEN
         WRITE(IU06,*) '   SUB. WAMODEL: START SOURCE INTEGRATION'
         WRITE(IU06,*) '      START DATE IS CDTSOU = ',CDTSOU
      END IF
   
      IF (CDTSOE.GE.CDATEWO) THEN         !! NEW WINDS IF NEEDED
         CALL GET_WIND (CDATEWO)  
      END IF

      CALL IMPLSCH (FL3, U10, UDIR, TAUW, USTAR, Z0, DEPTH, INDEP) 

      CDTSOU = CDTSOE                     !! UPDATE SOURCE TIME.
      CALL INCDATE(CDTSOE,IDELT)     

   END DO PHYSICS

   IF (ITEST.GE.2) WRITE(IU06,*) '   SUB. WAMODEL: INTEGRATION FINISHED'

!*    1.4 INPUT OF BOUNDARY VALUES.
!         -------------------------

   IF (FINE) THEN
      CALL BOUNDARY_INPUT (FL3)
      IF (ITEST.GE.2) THEN
         WRITE(IU06,*) '   SUB. WAMODEL: BOUNDARY VALUES INSERTED'
      END IF
   END IF

!*    1.5 SET SPECTRA AT ICE POINTS TO ZERO.
!         ----------------------------------

   IF (ICE_RUN) THEN
      CALL PUT_ICE (FL3, 0.)
      IF (ITEST.GE.2) WRITE(IU06,*) '   SUB. WAMODEL: ICE INSERTED'
   END IF

!*    1.6 OUTPUT OF BOUNDARY POINTS.
!        --------------------------

   IF (COARSE) THEN
      CALL BOUNDARY_OUTPUT (FL3)
      IF (ITEST.GE.2) THEN
         WRITE(IU06,*) '   SUB. WAMODEL: BOUNDARY OUTPUT (COURSE GRID) DONE'
      END IF
   END IF

!*    1.7 MODEL OUTPUT TO DISK AND/OR PRINTED.
!         ------------------------------------

   IF (CDTINTT.EQ.CDTPRO .OR. CDTSPT.EQ.CDTPRO) THEN
      CALL MODEL_OUTPUT_CONTROL (FL3, IU20, FILE20,IU25, FILE25)
      IF (ITEST.GE.2) WRITE(IU06,*) '   SUB. WAMODEL: MODEL_OUTPUT_CONTROL DONE'
   END IF

!*    1.8 SAVE OUTPUT AND RECOVERY FILES WHEN TIME REACHES THE SAVE DATE.
!         ---------------------------------------------------------------

   IF (CDTRES.EQ.CDTPRO.OR.CDATEE.EQ.CDTPRO) THEN
      IF (IREST.EQ.1 .OR. IREST.EQ.3) CALL SAVE_RESTART (IU17, FILE17)
   END IF

!*    1.9 PRINT TIME.
!         -----------

   WRITE (IU06,'(/,3X,''!!!!!!!!!!!!!! WAVE FIELDS INTEGRATED  DATE IS: '',    &
&                A14,''  !!!!!!!!!!!!!! '')') CDTPRO
WRITE (*,*) 'end of DO PROP', CDTPRO

END DO PROP

! ---------------------------------------------------------------------------- !

END SUBROUTINE WAMODEL

